dependencies {

    implementation project(":user-access-public-model")
    implementation project(":user-access-internal-model")
    implementation project(":user-access-commons")
    implementation project(":user-access-errors")


    implementation(group: 'com.amazonaws', name: 'aws-java-sdk-dynamodb', version: project.ext.awsSdkVersion) {
        because("This module contains the Dynamo access details and will be used by " +
                "any the handlers")

        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jsr310'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'com.fasterxml.jackson.module', module: 'jackson-module-parameter-names'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
    }


    api (group: 'com.amazonaws', name: 'aws-java-sdk-secretsmanager', version: project.ext.awsSdkVersion) {
        because("We want to have this library in the AWS::Serverless::LayerVersion resource" +
                "and do not want to have duplicates of this lib for each handler. ")
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jsr310'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'com.fasterxml.jackson.module', module: 'jackson-module-parameter-names'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
    }

    implementation(group: 'org.slf4j', name: 'slf4j-api', version: '1.8.0-beta4') {
        because "Because we use the library directly to define loggers in our code"
    }

    api group: 'com.github.BIBSYSDEV', name: 'nva-commons', version: project.ext.nvaCommonsVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: project.ext.jacksonVersion

    runtimeOnly(group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: project.ext.jacksonVersion)

    testImplementation project(':user-access-testing')
}

configurations.testImplementation.canBeResolved = true

task copyNativeDeps(type: Copy) {
    from(configurations.testImplementation) {
        include "*.dylib"
        include "*.so"
        include "*.dll"
    }
    into 'build/dynamodb-local'
}

test.dependsOn copyNativeDeps
test.doFirst {
    systemProperty "java.library.path", 'build/dynamodb-local'
}

// Build and package user-access-service and dependencies for Lambda Layer

configurations.api.canBeResolved = true

task copyDepsForLayer(type: Copy) {
    from(configurations.api) {
    }
    into 'build/libs'
}

task buildZipForLayer(type: Zip) {
    from('build/libs') {
    }
    into('java/lib')
}

buildZipForLayer.dependsOn copyDepsForLayer
build.dependsOn buildZipForLayer